/*
 * Charlatano is a premium CS:GO cheat ran on the JVM.
 * Copyright (C) 2016 Thomas Nappo, Jonathan Beaudoin
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
	ext.kotlin_version = '1.1-M03'
	ext.quasar_version = '0.7.7'
	ext.gdxVersion = '1.9.4'

	repositories {
		mavenCentral()
		maven { url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id "us.kirchmeier.capsule" version "1.0.2"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

group 'com.charlatano'
version '0.5.0'

mainClassName = 'com.charlatano.Charlatano'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
	mavenCentral()
	maven { url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations {
	quasar
	capsule
}

dependencies {
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version

	compile group: 'org.jire.arrowhead', name: 'arrowhead', version: '1.2.1'

	capsule group: 'co.paralleluniverse', name: 'capsule', version: '1.0.3'
	quasar group: 'co.paralleluniverse', name: 'quasar-core', version: quasar_version

	compile group: 'co.paralleluniverse', name: 'quasar-core', version: quasar_version
	compile group: 'co.paralleluniverse', name: 'quasar-actors', version: quasar_version
	compile group: 'co.paralleluniverse', name: 'quasar-kotlin', version: quasar_version

	compile "com.badlogicgames.gdx:gdx:$gdxVersion"
	compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"

	compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
	compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"

	compile group: 'net.java.dev.jna', name: 'jna', version: '4.3.0-20161031.222201-3'
	compile group: 'net.java.dev.jna', name: 'jna-platform', version: '4.3.0-20161031.222304-3'

	compile group: 'it.unimi.dsi', name: 'fastutil', version: '7.0.13'
}

task capsule(type: FatCapsule) {
	applicationClass mainClassName

	classifier = 'release'
	reallyExecutable

	capsuleManifest {
		systemProperties['co.paralleluniverse.fibers.detectRunawayFibers'] = 'false'
		systemProperties['co.paralleluniverse.fibers.DefaultFiberPool.parallelism'] = '1'
		//args = ["-javaagent:${configurations.quasar.iterator().next()}"]
		minJavaVersion = '1.8.0'
		javaAgents = [(++configurations.quasar.iterator()).getName()]
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.1' //version required
}

run {
	jvmArgs "-javaagent:${(++configurations.quasar.iterator())}"
}

sourceSets {
	main.java.srcDirs += 'src/main/kotlin'
}
